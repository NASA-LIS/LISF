 ! FERRET v6.96 Script
 ! >ferret -gif -script ferret_LIS_HIST_Noah.3.3_output.jnl [LIS_HIST input file] [Output prefix]
 ! Author: Daniel Rosen
 ! Organization: NESII/CIRES/NOAA
 ! Email: daniel.rosen@noaa.gov
 ! Date: 2017-01-24

CANCEL MODE LOGO

DEFINE SYMBOL outlabel "$2"

use $1

SET VARIABLE/TITLE="Longitude" lon[d=1]
SET VARIABLE/TITLE="Latitude" lat[d=1]

! List single level forcing variables

LET fnames = {   \
"Psurf_f_inst",  \
"Tair_f_inst",   \
"EWind_f_inst",  \
"LWdown_f_inst", \
"NWind_f_inst",  \
"Rainf_f_tavg",  \
"SWdown_f_inst", \
"Qair_f_inst"    \
}

LET ftitles = {       \
"Air Pressure",       \
"Air Temperature",    \
"Eastward Wind",      \
"Longwave Flux",      \
"Northward Wind",     \
"Precipitation Flux", \
"Shortwave Flux",     \
"Specific Humidity"   \
}

LET fminvals  = {  65000, 250,-30, 100,-30, 0.0000,   0, 0.000 }
LET fmaxvals  = { 105000, 310, 30, 450, 30, 0.0100, 500, 0.025 }
LET fstepvals = {   1000,   2,  2,  10,  2, 0.0002,  10, 0.001 }
LET fminoutlr = {  10000, 200,-60,  50,-60, 0.0000,   0, 0.000 }
LET fmaxoutlr = { 200000, 350, 60, 600, 60, 0.1000, 999, 0.025 }

! Create SHADE plots for single level forcing variables
SAY *** Plot single level forcing variables ***
REPEAT/RANGE=1:`fnames,return=isize`/NAME=vindex ( \
DEFINE SYMBOL ii `vindex`; \
DEFINE SYMBOL vtitle `ftitles[i=($ii)]`  ; \
DEFINE SYMBOL vname  `fnames[i=($ii)]`   ; \
DEFINE SYMBOL vmin   `fminvals[i=($ii)]` ; \
DEFINE SYMBOL vmax   `fmaxvals[i=($ii)]` ; \
DEFINE SYMBOL vstep  `fstepvals[i=($ii)]`; \
DEFINE SYMBOL vmnol  `fminoutlr[i=($ii)]`; \
DEFINE SYMBOL vmxol  `fmaxoutlr[i=($ii)]`; \
SET VARIABLE/BAD=-9999/TITLE="($vtitle)" ($vname)[d=1]; \
SAY ($vtitle) (outliers,min,max,outliers)=\
(($vmnol),($vmin),($vmax),($vmxol)); \
SHADE/LEVELS="(($vmnol))(($vmin),($vmax),($vstep))(($vmxol))"/\
KEY=CONTINUOUS/TITLE="LIS Forcing ($vtitle)" \
($vname)[d=1], lon[d=1], lat[d=1]; \
FRAME/FILE=($outlabel)_forcing_($vname).gif \
)

! List single level fields output variables

LET onames = {    \
"Albedo_inst",    \
"Qg_tavg",        \
"SnowCover_inst", \
"SnowDepth_inst", \
"AvgSurfT_tavg",  \
"Qle_tavg",       \
"Qh_tavg"         \
} ! To be added "effective_mixing_ratio"

LET otitles = {               \
"Albedo",                     \
"Downward Heat Flux in Soil", \
"Snow Area Fraction",         \
"Snow Depth",                 \
"Surface Temperature",        \
"Upward Latent Heat Flux",    \
"Upward Sensible Heat Flux"   \
} ! To be added "Effective Mixing Ratio

LET ominvals  = { 0.00,-250, 0.00,  0.00, 250,-100,-200 }
LET omaxvals  = { 0.60, 100, 1.00,  1.00, 310, 200, 200 }
LET ostepvals = { 0.01,   5, 0.02,  0.02,   2,   5,   5 }
LET ominoutlr = { 0.00,-999, 0.00,  0.00, 200,-500,-500 }
LET omaxoutlr = { 0.99, 999, 1.00,999.00, 350, 500, 500 }

! Create SHADE plots for single level output variables
SAY *** Plot single level output variables ***
REPEAT/RANGE=1:`onames,return=isize`/NAME=vindex ( \
DEFINE SYMBOL ii `vindex`; \
DEFINE SYMBOL vtitle `otitles[i=($ii)]`  ; \
DEFINE SYMBOL vname  `onames[i=($ii)]`   ; \
DEFINE SYMBOL vmin   `ominvals[i=($ii)]` ; \
DEFINE SYMBOL vmax   `omaxvals[i=($ii)]` ; \
DEFINE SYMBOL vstep  `ostepvals[i=($ii)]`; \
DEFINE SYMBOL vmnol  `ominoutlr[i=($ii)]`; \
DEFINE SYMBOL vmxol  `omaxoutlr[i=($ii)]`; \ 
SET VARIABLE/BAD=-9999/TITLE="($vtitle)" ($vname)[d=1]; \
SAY ($vtitle) (outliers,min,max,outliers)=\
(($vmnol),($vmin),($vmax),($vmxol)); \
SHADE/LEVELS="(($vmnol))(($vmin),($vmax),($vstep))(($vmxol))"/\
KEY=CONTINUOUS/TITLE="LIS ($vtitle)" \
($vname)[d=1], lon[d=1], lat[d=1]; \
FRAME/FILE=($outlabel)_output_($vname).gif \
)

! List multi-level output variables

LET onamesml = {  \
"SmLiqFrac_inst", \
"SoilMoist_inst", \
"SoilTemp_inst"   \
}

LET otitlesml = {                   \
"Liquid Fraction of Soil Moisture", \
"Soil Moisture",                    \
"Soil Temperature"                  \
}

LET ominvalsml  = { 0.400, 0.00, 250 }
LET omaxvalsml  = { 0.500, 0.50, 310 }
LET ostepvalsml = { 0.002, 0.01,   2 }
LET ominoutlrml = { 0.000, 0.00, 200 }
LET omaxoutlrml = { 1.000, 1.00, 350 }

! Create SHADE plots for multi-level output variables
SAY *** Plot multi level output variables ***
REPEAT/RANGE=1:`onamesml,return=isize`/NAME=vindex ( \
DEFINE SYMBOL ii `vindex`; \
DEFINE SYMBOL vtitle `otitlesml[i=($ii)]`; \
DEFINE SYMBOL vname  `onamesml[i=($ii)]`; \
SET VARIABLE/BAD=-9999/TITLE="($vtitle)" ($vname)[d=1]; \
REPEAT/K=1:`($vname)[d=1],return=ksize` ( \
DEFINE SYMBOL lname `k`; \
DEFINE SYMBOL ii `vindex`; \
DEFINE SYMBOL vtitle `otitlesml[i=($ii)]`  ; \
DEFINE SYMBOL vname  `onamesml[i=($ii)]`   ; \
DEFINE SYMBOL vmin   `ominvalsml[i=($ii)]` ; \
DEFINE SYMBOL vmax   `omaxvalsml[i=($ii)]` ; \
DEFINE SYMBOL vstep  `ostepvalsml[i=($ii)]`; \
DEFINE SYMBOL vmnol  `ominoutlrml[i=($ii)]`; \
DEFINE SYMBOL vmxol  `omaxoutlrml[i=($ii)]`; \
SAY ($vtitle) K=($lname) (outliers,min,max,outliers)=\
(($vmnol),($vmin),($vmax),($vmxol)); \
SHADE/K=`k`/LEVELS="(($vmnol))(($vmin),($vmax),($vstep))(($vmxol))"/\
KEY=CONTINUOUS/TITLE="LIS ($vtitle) Level ($lname)" \
($vname)[d=1], lon[d=1], lat[d=1]; \
FRAME/FILE=($outlabel)_output_($vname)_Lv($lname).gif \
))

exit
