 ! FERRET v6.96 Script
 ! >ferret -gif -script ferret_NUOPC_export.jnl [Grid File] [Restart File]
 ! Author: Daniel Rosen
 ! Organization: NESII/CIRES/NOAA
 ! Email: daniel.rosen@noaa.gov
 ! Date: 2017-02-27

CANCEL MODE LOGO

SAY *** Generating NUOPC restart SHADE plots ***
SAY 

! Load grid file and compute output file label
USE $1
SET VARIABLE/TITLE="Longitude" lon_center[d=1]; \
SET VARIABLE/TITLE="Latitude" lat_center[d=1]; \

! Load restart file and compute output file label
USE $2
DEFINE SYMBOL dname `"$2"`
DEFINE SYMBOL fnlen `STRLEN("($dname)")`
DEFINE SYMBOL ext `STRRINDEX("($dname)",".nc")`
DEFINE SYMBOL rlabel `STRRINDEX("($dname)","_RSTRT_")`
DEFINE SYMBOL clabel `SUBSTRING("($dname)",1,($rlabel)-1)`
DEFINE SYMBOL dlabel `SUBSTRING("($dname)",($rlabel)+7,($fnlen)-($rlabel)-9)`
SAY ($clabel)
SAY ($dlabel)

! Print datasets
SHOW DATA/BRIEF

! Define single level variables
LET fnames = { \
"qs",          \
"qsb",         \
"smfracl1",    \
"smfracl2",    \
"smfracl3",    \
"smfracl4",    \
"smliqfracl1", \
"smliqfracl2", \
"smliqfracl3", \
"smliqfracl4", \
"soiltempl1",  \
"soiltempl2",  \
"soiltempl3",  \
"soiltempl4"   \
}

! Define single level variable titles
LET ftitles = {                    \
"Surface Runoff",                  \
"Subsurface Runoff",               \
"Soil Moisture Content LV1",       \
"Soil Moisture Content LV2",       \
"Soil Moisture Content LV3",       \
"Soil Moisture Content LV4",       \
"Fraction of Liquid Moisture LV1", \
"Fraction of Liquid Moisture LV2", \
"Fraction of Liquid Moisture LV3", \
"Fraction of Liquid Moisture LV4", \
"Soil Temperature LV1",            \
"Soil Temperature LV2",            \
"Soil Temperature LV3",            \
"Soil Temperature LV4"             \
}

! Define single level forcing variable scales
LET fminvals  = { 0,        0,        0.2,  0.2,  0.2,  0.2,  0.2,  0.2,  0.2,  0.2,  260, 260, 260, 260 }
LET fmaxvals  = { 0.001,    0.00025,  0.5,  0.5,  0.5,  0.5,  0.5,  0.5,  0.5,  0.5,  320, 320, 320, 320 }
LET fstepvals = { 0.000025, 0.000005, 0.01, 0.01, 0.01, 0.01, 0.01, 0.01, 0.01, 0.01, 1,   1,   1,   1   }
LET fminoutlr = { 0,        0,        0,    0,    0,    0,    0,    0,    0,    0,    250, 250, 250, 250 } 
LET fmaxoutlr = { 0.01,     0.001,    1,    1,    1,    1,    1,    1,    1,    1,    350, 350, 350, 350 }

! Create SHADE plots for single level forcing variables
SAY
SAY *** Plotting single level forcing variables ***
REPEAT/RANGE=1:`fnames,return=isize`/NAME=vctr ( \
DEFINE SYMBOL vindex `vctr`; \
DEFINE SYMBOL vtitle `ftitles[i=($vindex)]`  ; \
DEFINE SYMBOL vname  `fnames[i=($vindex)]`   ; \
DEFINE SYMBOL vmin   `fminvals[i=($vindex)]` ; \
DEFINE SYMBOL vmax   `fmaxvals[i=($vindex)]` ; \
DEFINE SYMBOL vstep  `fstepvals[i=($vindex)]`; \
DEFINE SYMBOL vmnol  `fminoutlr[i=($vindex)]`; \
DEFINE SYMBOL vmxol  `fmaxoutlr[i=($vindex)]`; \
SET VARIABLE/BAD=-9999/TITLE="($vtitle)" ($vname)[d=2]; \
SAY ($vtitle) (outliers,min,max,outliers)=\
(($vmnol),($vmin),($vmax),($vmxol)); \
SHADE/LEVELS="(($vmnol))(($vmin),($vmax),($vstep))(($vmxol))"/\
KEY=CONTINUOUS/TITLE="NUOPC State Variable ($vtitle)" \
($vname)[d=2], lon_center[d=1], lat_center[d=1]; \
FRAME/FILE=plot_($clabel)_($dlabel)_($vname).gif \
)

! Create compressed tar archive of all .gif files
SAY
SAY *** Creating plots.tar.gz archive ***
SPAWN tar -czf plots.tar.gz *.gif

SAY

exit
